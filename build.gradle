plugins {
    // this plugin gets applied only to this project
    id 'groovy-gradle-plugin'

    // publishing to gradle plugin portal
    id "com.gradle.plugin-publish" version "0.14.0"
}

repositories {
    gradlePluginPortal()
}

dependencies {
    // the dependency coordinates for "com.gradle.plugin-publish"
    implementation "com.gradle.publish:plugin-publish-plugin:0.14.0"
}


group = "com.medly"
description = "A Gradle Plugin to Build Gradle Plugins"

pluginBundle {
    website = 'https://github.com/medly/inception'
    vcsUrl = 'https://github.com/medly/inception'
    description = project.description
    tags = ['gradle', 'plugin', 'convention']

    mavenCoordinates {
        groupId = project.group.toString()
        artifactId = project.name
        version = project.version.toString()
    }
}


pluginDescriptors { // we are configuring task here, this is not an extension block
    afterEvaluate { // after this task is evaluated
        declarations.get().each { PluginDeclaration declaration ->
            declaration.setDisplayName(declaration.id) // set it to id for now
            declaration.setDescription(project.description)
        }
    }
}


